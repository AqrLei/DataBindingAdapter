plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.github.dcendents.android-maven'
    id 'com.jfrog.bintray'
    id 'org.jetbrains.dokka-android'
}
android {
    compileSdkVersion buildVersion.compile


    defaultConfig {
        minSdkVersion buildVersion.min
        targetSdkVersion buildVersion.target
        versionCode 2
        versionName "0.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    dataBinding {
        enabled = true
    }
    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.androidx.appcompat
    implementation deps.material
    implementation deps.androidx.paging
    implementation deps.kotlin.coroutines
    implementation deps.kotlin.stdlib
    testImplementation deps.junit
    androidTestImplementation deps.androidx.runner
    androidTestImplementation deps.androidx.espresso
}

task makeJar(type: Copy) {
    delete 'build/outputs/jar/aqrlei_databindingadapter.jar'

    from('build/intermediates/packaged-classes/release/')
    into('build/outputs/jar')

    include('classes.jar')

    rename('classes.jar', 'aqrlei_databindingadapter.jar')
}
makeJar.dependsOn(build)

def siteUrl = 'https://github.com/AqrLei/DataBindingAdapter'

def gitUrl = 'https://github.com/AqrLei/DataBindingAdapter.git'

def libName = "databindingadapter"


group = "com.aqrlei.opensource"

version = "0.0.2"

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        configuration = configurations.archives

        pom {
            project {
                packaging 'aar'
                name 'databindingadapter'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'aqrlei'
                        name 'AqrLei'
                        email 'aqrdeveloper@gmail.com'
                    }
                }
                scm {
                    connection "scm:git:git://github.com/AqrLei/DataBindingAdapter.git"
                    url siteUrl
                }
            }
        }
    }
}

task androidSourcesJar(type: Jar) {
    from android.sourceSets.main.java.source
    classifier = 'sources'
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/documentation"
}
task androidJavadocsJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidJavadocsJar
    archives androidSourcesJar
}

tasks.withType(Javadoc) {//防止编码问题
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")

    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "openSource"
        name = libName
        desc = 'databindingadapter'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}